plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '[7.0,7.1)'
//	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = '1.2.2'
group = 'suszombification'

base {
	archivesName = 'suszombification-1.20.2'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

processResources {
    exclude ".cache"
    duplicatesStrategy = 'include'
}

sourceSets.main.resources {
    srcDir 'src/generated/resources' //include generated files
}

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    configureEach {
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        workingDirectory project.file('run/client')
    }

    server {
        workingDirectory project.file('run/server')
        programArgument '-nogui'
    }
	
	data {
		workingDirectory project.file('run/data')
		programArguments.addAll '--mod', 'suszombification', '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
	}
}

dependencies {
    implementation 'net.neoforged:neoforge:20.2.59-beta'
}

tasks.named('jar', Jar).configure {
    exclude('suszombification/datagen/**') //exclude files from the built jar that are only used to generate the assets

    manifest {
        attributes([
                "Specification-Title"     : "Suspicious Zombification",
                "Specification-Vendor"    : "Redstone_Dubstep, bl4ckscor3",
                "Specification-Version"   : "${version}",
                "Implementation-Title"    : "Suspicious Zombification",
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Redstone_Dubstep, bl4ckscor3"
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}
