plugins {
	id 'eclipse'
	id 'idea'
	id 'net.minecraftforge.gradle' version '[6.0,6.2)'
	id 'org.spongepowered.mixin' version '0.7.+'
	id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = '1.2.1'
group = 'suszombification'

base {
	archivesName = 'suszombification-1.20.1'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

processResources {
    exclude ".cache"
    duplicatesStrategy = 'include'
}

sourceSets.main.resources {
    srcDir 'src/generated/resources' //include generated files
}

minecraft {
	mappings channel: 'parchment', version: '2023.09.03-1.20.1'
	accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
	copyIdeResources = true

    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.console.level', 'debug'
            args '-mixin.config=suszombification.mixins.json'

            mods {
                suszombification {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.console.level', 'debug'
            args '-nogui', '-mixin.config=suszombification.mixins.json'

            mods {
                suszombification {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'suszombification', '--all', '--output', file('src/generated/resources/')

            mods {
                suszombification {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.20.1-47.1.3'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    add sourceSets.main, "suszombification.refmap.json"
}

tasks.named('jar', Jar).configure {
    exclude('suszombification/datagen/**') //exclude files from the built jar that are only used to generate the assets

    manifest {
        attributes([
                "Specification-Title"     : "Suspicious Zombification",
                "Specification-Vendor"    : "Redstone_Dubstep, bl4ckscor3",
                "Specification-Version"   : "${version}",
                "Implementation-Title"    : "Suspicious Zombification",
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Redstone_Dubstep, bl4ckscor3",
                "MixinConfigs": "suszombification.mixins.json"
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
	options.release = 17
}
