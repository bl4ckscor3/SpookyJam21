plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.0.+'
}

version = '1.2.7'
group = 'suszombification'

base {
	archivesName = 'suszombification-1.21.4'
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

processResources {
    exclude ".cache"
    duplicatesStrategy = 'include'
}

sourceSets.main.resources {
    srcDir 'src/generated/resources' //include generated files
}

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

runs {
    configureEach {
        systemProperty 'forge.logging.console.level', 'debug'
        modSource project.sourceSets.main
    }

    client {
        workingDirectory project.file('run/client')
    }

    server {
        workingDirectory project.file('run/server')
        argument '-nogui'
    }
	
	clientData {
		workingDirectory project.file('run/data')
		arguments.addAll '--mod', 'suszombification', '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
	}
}

dependencies {
	implementation 'net.neoforged:neoforge:21.4.12-beta'
}

tasks.named('jar', Jar).configure {
    exclude('suszombification/datagen/**') //exclude files from the built jar that are only used to generate the assets

    manifest {
        attributes([
                "Specification-Title"     : "Suspicious Zombification",
                "Specification-Vendor"    : "Redstone_Dubstep, bl4ckscor3",
                "Specification-Version"   : "${version}",
                "Implementation-Title"    : "Suspicious Zombification",
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Redstone_Dubstep, bl4ckscor3"
        ])
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
	options.release = 21
}
